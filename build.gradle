plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
    id 'de.jjohannes.extra-java-module-info' version '0.11'
}

group 'fr.xahla'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'fr.xahla.musicx'
    mainClass = 'fr.xahla.musicx.HelloApplication'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1')
    implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')
    implementation('eu.hansolo:tilesfx:11.48') {
        exclude(group: 'org.openjfx')
    }

    implementation('net.jthink:jaudiotagger:3.0.1')
    implementation('org.json:json:20240303')

    implementation('io.github.mkpaz:atlantafx-base:2.0.1')

    /* --- Hibernate --- */
    implementation(platform("org.hibernate.orm:hibernate-platform:6.4.4.Final"))

    implementation("org.hibernate.orm:hibernate-core")
    implementation('org.hibernate.common:hibernate-commons-annotations:6.0.6.Final')
    implementation("jakarta.transaction:jakarta.transaction-api")

    implementation('jakarta.enterprise:jakarta.enterprise.cdi-api:4.0.1')
    implementation('jakarta.xml.bind:jakarta.xml.bind-api:4.0.2')
    implementation('org.jboss.logging:jboss-logging:3.5.3.Final')
    implementation('com.fasterxml:classmate:1.7.0')

    implementation('net.bytebuddy:byte-buddy:1.14.12')
    /* ------------------ */

    implementation('com.h2database:h2:2.2.224')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

extraJavaModuleInfo {
    module("jaudiotagger-3.0.1.jar", "jaudiotagger", "3.0.1") {
        requires("java.logging")

        exports("org.jaudiotagger.audio")
        exports("org.jaudiotagger.tag")
    }
}